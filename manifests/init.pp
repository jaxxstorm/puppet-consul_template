# == Class: consul_template
#
# Installs, configures, and manages consul-template
#
# === Parameters
#
# [*version*]
#   Specify version of consul-template binary to download.
#
# [*install_method*]
#   Defaults to `url` but can be `package` if you want to install via a system package.
#
# [*package_name*]
#   Only valid when the install_method == package. Defaults to `consul-template`.
#
# [*package_ensure*]
#   Only valid when the install_method == package. Defaults to `latest`.
#
# [*extra_options*]
#   Extra arguments to be passed to the consul-template agent
#
# [*init_style*]
#   What style of init system your system uses.
#
# [*config_mode*]
#   Set config file mode
#
# [*purge_config_dir*]
#   Purge config files no longer generated by Puppet
#
# [*user*]
#   Name of a user to use for dir and file perms. Defaults to consul-template.
#
# [*group*]
#   Name of a group to use for dir and file perms. Defaults to consul-template.
#
# [*manage_user*]
#   User is managed by this module. Defaults to `false`.
#
# [*manage_group*]
#   Group is managed by this module. Defaults to `false`.
#
# [*restart_on_change*]
#   Determines whethere to restart consul template when $config_hash changes
#   Defaults to tru
#
# [*watches*]
#   A hash of watches - allows greater Hiera integration. Defaults to `{}`.
#
# [*config_hash*]
#   Use this to populate the JSON config file for consul.
#
# [*pretty_config*]
#   Generates a human readable JSON config file. Defaults to `false`.
#
# [*pretty_config_indent*]
#   Toggle indentation for human readable JSON file. Defaults to `4`.

class consul_template (
  $purge_config_dir       = true,
  $config_mode            = $consul_template::params::config_mode,
  $bin_dir                = '/usr/local/bin',
  $arch                   = $consul_template::params::arch,
  $version                = $consul_template::params::version,
  $install_method         = $consul_template::params::install_method,
  $os                     = $consul_template::params::os,
  $download_url           = undef,
  $download_url_base      = $consul_template::params::download_url_base,
  $download_extension     = $consul_template::params::download_extension,
  $package_name           = $consul_template::params::package_name,
  $package_ensure         = $consul_template::params::package_ensure,
  $config_dir             = '/etc/consul-template',
  $consul                 = 'localhost:8500',
  $extra_options          = '',
  $service_enable         = true,
  $service_ensure         = 'running',
  $restart_on_change      = true,
  $manage_service         = true,
  $init_style             = $consul_template::params::init_style,
  $log_level              = $consul_template::params::log_level,
  $logrotate_compress     = 'nocompress',
  $logrotate_files        = 4,
  $logrotate_on           = false,
  $logrotate_period       = 'daily',
  $user                   = $consul_template::params::user,
  $group                  = $consul_template::params::group,
  $manage_user            = $consul_template::params::manage_user,
  $manage_group           = $consul_template::params::manage_group,
  $watches                = {},
  $config_hash            = {},
  $config_defaults        = {},
  $pretty_config          = false,
  $pretty_config_indent   = 4,
) inherits ::consul_template::params {

  validate_bool($purge_config_dir)
  validate_string($user)
  validate_string($group)
  validate_bool($manage_user)
  validate_bool($manage_group)
  validate_bool($manage_service)
  validate_bool($restart_on_change)
  validate_hash($watches)
  validate_hash($config_hash)

  $config_hash_defaults = { 'consul' => $consul }
  # Rightmost hash wins
  # So if consul exists in $config_hash, we override it
  $config_hash_real = deep_merge($config_hash_defaults, $config_defaults, $config_hash) 
  validate_hash($config_hash_real)
  
  $real_download_url = pick($download_url, "${download_url_base}${version}/${package_name}_${version}_${os}_${arch}.${download_extension}")

  if $watches {
    create_resources(consul_template::watch, $watches)
  }

  $notify_service = $restart_on_change ? {
    true    => Class['consul_template::service'],
    default => undef,
  }
  anchor { '::consul_template::begin': } ->
  class { '::consul_template::install': } ->
  class { '::consul_template::config':
    config_hash  => $config_hash_real,
    purge        => $purge_config_dir,
    notify       => $notify_service,
  } ->
  class { '::consul_template::service': } ->
  anchor { '::consul_template::end': }

  class { '::consul_template::logrotate':
    logrotate_compress => $logrotate_compress,
    logrotate_files    => $logrotate_files,
    logrotate_on       => $logrotate_on,
    logrotate_period   => $logrotate_period,
    before             => Anchor['::consul_template::end'],
  }
}
